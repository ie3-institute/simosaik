plugins {
  id 'java' // java support
  id 'scala' // scala support
  id 'maven-publish' // publish to a maven repo (local or mvn central, has to be defined)
  id "com.github.johnrengelman.shadow" version "8.1.1"
  id 'com.diffplug.spotless' version '6.25.0'// code format
  id "de.undercouch.download" version "5.6.0" // downloads plugin
}

ext {
  //version (changing these should be considered thoroughly!)
  javaVersion = JavaVersion.VERSION_17

  scalaVersion = '2.13'
  scalaBinaryVersion = '2.13.12'
  pekkoVersion = "1.1.1"

  scriptsLocation = 'gradle' + File.separator + 'scripts' + File.separator // location of script plugins
}

group = 'com.github.ie3-institute'
description = 'simosaik'

java {
  sourceCompatibility = javaVersion
  targetCompatibility = javaVersion
}

apply from: scriptsLocation + 'spotless.gradle'

configurations {
  scalaCompilerPlugin
}

repositories {
  mavenCentral() // searches in Sonatype's central repository
  maven { url 'https://s01.oss.sonatype.org/content/repositories/snapshots' } // sonatype snapshot repo
  maven { url 'https://packages.confluent.io/maven' } // confluent repo (kafka)
}

dependencies {
  implementation 'com.opencsv:opencsv:5.5.2'
  implementation 'tech.units:indriya:2.2' // quantities

  /* Mosaik dependencies */
  implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.9.9.3'
  implementation 'com.googlecode.json-simple:json-simple:1.1.1'
  implementation files('libs/mosaik-api-java-3.0.jar')

  // ieÂ³ internal repository
  implementation('com.github.ie3-institute:PowerSystemUtils:2.2.1') {
    exclude group: 'org.apache.logging.log4j'
    exclude group: 'org.slf4j'
    /* Exclude our own nested dependencies */
    exclude group: 'com.github.ie3-institute'
  }
  implementation('com.github.ie3-institute:PowerSystemDataModel:6.0.0') {
    exclude group: 'org.apache.logging.log4j'
    exclude group: 'org.slf4j'
    /* Exclude our own nested dependencies */
    exclude group: 'com.github.ie3-institute'
  }
  implementation('com.github.ie3-institute:simonaAPI:0.7.0') {
    exclude group: 'org.apache.logging.log4j'
    exclude group: 'org.slf4j'
    /* Exclude our own nested dependencies */
    exclude group: 'edu.ie3'
  }

  /* logging */
  implementation platform('org.apache.logging.log4j:log4j-bom:2.24.2')
  implementation 'org.apache.logging.log4j:log4j-api' // log4j
  implementation 'org.apache.logging.log4j:log4j-core' // log4j
  implementation 'org.apache.logging.log4j:log4j-slf4j-impl' // log4j -> slf4j

  /* config */
  implementation 'com.typesafe:config:1.4.3'


  /* CORE Pekko */
  implementation "org.apache.pekko:pekko-actor_${scalaVersion}:${pekkoVersion}"
  implementation "org.apache.pekko:pekko-actor-typed_${scalaVersion}:${pekkoVersion}"
  implementation "org.apache.pekko:pekko-slf4j_${scalaVersion}:${pekkoVersion}"
}

tasks.withType(JavaCompile) {
  options.encoding = 'UTF-8'
}

shadowJar {
  zip64 = true
  archiveBaseName.set('simosaik')
  archiveClassifier.set('')
}
