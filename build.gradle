plugins {
  id 'java' // java support
  id 'groovy'
  id 'maven-publish' // publish to a maven repo (local or mvn central, has to be defined)
  id "com.gradleup.shadow" version "8.3.6"
  id 'com.diffplug.spotless' version '6.25.0'// code format
  id "de.undercouch.download" version "5.6.0" // downloads plugin
  id 'kr.motd.sphinx' version '2.10.1' // documentation generation
}

ext {
  //version (changing these should be considered thoroughly!)
  javaVersion = JavaVersion.VERSION_17
  scriptsLocation = 'gradle' + File.separator + 'scripts' + File.separator // location of script plugins
}

group = 'com.github.ie3-institute'
description = 'simosaik'

java {
  sourceCompatibility = javaVersion
  targetCompatibility = javaVersion
}

apply from: scriptsLocation + 'spotless.gradle'
apply from: scriptsLocation + 'documentation.gradle'

configurations {
  scalaCompilerPlugin
}

repositories {
  mavenCentral() // searches in Sonatype's central repository
  maven { url 'https://s01.oss.sonatype.org/content/repositories/snapshots' } // sonatype snapshot repo
  maven { url 'https://packages.confluent.io/maven' } // confluent repo (kafka)
  maven { url "https://gitlab.com/api/v4/projects/16916947/packages/maven" }
}

dependencies {
  implementation 'tech.units:indriya:2.2.3' // quantities

  /* Mosaik dependencies */
  implementation("de.offis.mosaik.api:mosaik-api-java:3.1")

  // ieÂ³ internal repository
  implementation('com.github.ie3-institute:PowerSystemUtils:3.1.0') {
    exclude group: 'org.apache.logging.log4j'
    exclude group: 'org.slf4j'
    /* Exclude our own nested dependencies */
    exclude group: 'com.github.ie3-institute'
  }
  implementation('com.github.ie3-institute:PowerSystemDataModel:7.0.0') {
    exclude group: 'org.apache.logging.log4j'
    exclude group: 'org.slf4j'
    /* Exclude our own nested dependencies */
    exclude group: 'com.github.ie3-institute'
  }
  implementation('com.github.ie3-institute:simonaAPI:0.9.0') {
    exclude group: 'org.apache.logging.log4j'
    exclude group: 'org.slf4j'
    /* Exclude our own nested dependencies */
    exclude group: 'edu.ie3'
  }

  /* logging */
  implementation platform('org.apache.logging.log4j:log4j-bom:2.24.2')
  implementation 'org.apache.logging.log4j:log4j-api' // log4j
  implementation 'org.apache.logging.log4j:log4j-core' // log4j
  implementation 'org.apache.logging.log4j:log4j-slf4j-impl' // log4j -> slf4j

  /* config */
  implementation 'com.typesafe:config:1.4.3'


  /* testing */
  testImplementation 'org.spockframework:spock-core:2.3-groovy-4.0'
}

tasks.withType(JavaCompile).configureEach {
  options.encoding = 'UTF-8'
}

shadowJar {
  zip64 = true
  archiveBaseName.set('simosaik')
  archiveClassifier.set('')
}
